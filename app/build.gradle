plugins {
    id 'com.github.triplet.play' version '2.2.0'
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.codingbuffalo.aerialdream"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1 // Will be incremented automatically on release
        versionName "1.7"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("../AppSignature/keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties.keyAlias
                keyPassword keystoreProperties.keyPassword
                storeFile file(keystoreProperties.storeFile)
                storePassword keystoreProperties.storePassword
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'com.google.android.exoplayer:exoplayer-core:2.10.8'

    implementation 'com.android.support:leanback-v17:28.0.0'
    implementation 'com.android.support:preference-leanback-v17:28.0.0'

    testImplementation 'junit:junit:4.12'
}

preBuild.doFirst {
    // Fetch playlist files
    exec {
        executable "../fetch_playlists.sh"
    }
}

clean.doFirst {
    // Delete playlists
    delete fileTree(dir:'src/main/res/raw/', include: 'tvos**.json')
}

play {
    serviceAccountCredentials = rootProject.file("../AppSignature/play-store.json")
    track = 'beta'
    resolutionStrategy = "auto"
}
