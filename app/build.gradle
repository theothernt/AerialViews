apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.2"

    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = rootProject.file('../AppSignature/play-store.json')
        }
    }

    defaultConfig {
        applicationId "com.codingbuffalo.aerialdream"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 13
        versionName "1.3"

        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("../AppSignature/keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties.keyAlias
                keyPassword keystoreProperties.keyPassword
                storeFile file(keystoreProperties.storeFile)
                storePassword keystoreProperties.storePassword
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    implementation 'com.google.android.exoplayer:exoplayer-core:2.8.4'

    implementation 'com.android.support:leanback-v17:28.0.0-rc02'
    implementation 'com.android.support:preference-leanback-v17:28.0.0-rc02'

    testImplementation 'junit:junit:4.12'
}

play {
    track = 'beta'
}
